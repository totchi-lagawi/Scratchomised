plugins {
    id "java-library"
    id "com.gradleup.shadow" version '9.0.0-beta8'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.yaml:snakeyaml:2.3'
    //implementation "io.javalin:javalin:4.6.8"
    implementation "org.eclipse.jetty.websocket:websocket-server:9.4.57.v20241219"
    // Why is logging so complicated
    implementation "org.slf4j:slf4j-nop:2.1.0-alpha1"
    implementation "com.fasterxml.jackson.jr:jackson-jr-objects:2.18.3"
    // Shadow means it will be included at runtime, to prevent shadow to include it
    // And trust me you don't want that to happen
    // Look at the size of SweetHome3D's JAR
    shadow files("SweetHome3D-7.5.jar")
}

// Java 8 is the Java version SweetHome3D uses
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

// Minimize the resulting JAR, a bit
tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
  minimize {
    // But we need to say that this should be included since it isn't imported directly
    exclude(dependency("org.eclipse.jetty.websocket:websocket-server:.*"))
    exclude(dependency("org.slf4j:slf4j-nop:.*"))
  }
}
